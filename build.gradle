plugins {
    id 'java-library'
    id 'application' // need it for testOps
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    mavenCentral()
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
}

def junitVersion = "5.7.0"
def selenideVersion = "5.19.0"
def ownerVersion = "1.0.12"
def allureVersion = "2.13.8"
def aspectjweaverVersion = "1.9.6"
def allureTestopsVersion = "3.28.2"

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "com.codeborne:selenide:${selenideVersion}"
    testImplementation "org.aeonbits.owner:owner:${ownerVersion}"
    testImplementation "io.qameta.allure:allure-selenide:${allureVersion}"
    testImplementation "org.aspectj:aspectjweaver:${aspectjweaverVersion}"
    testImplementation "io.qameta.allure:allure-ee-junit-platform:${allureTestopsVersion}"
    testImplementation "io.rest-assured:rest-assured:4.3.1"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.12.2"
    testImplementation "io.qameta.allure:allure-rest-assured:${allureVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

tasks.withType(Test) {
    systemProperties += System.properties

    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}